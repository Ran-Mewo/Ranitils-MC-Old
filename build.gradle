plugins {
	id 'fabric-loom' version '0.12-SNAPSHOT'
	id 'maven-publish'
	id 'com.github.johnrengelman.shadow' version '7.0.0'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17
compileJava.options.encoding = "UTF-8"

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven { url 'https://jitpack.io' }

	// Cloth Config
	maven { url "https://maven.shedaniel.me/" }

	// Mod Menu
	maven { url "https://maven.terraformersmc.com/" }
}

configurations {
	shade
	implementation.extendsFrom shade

	modIncludeImplementation

	include.extendsFrom modIncludeImplementation
	modImplementation.extendsFrom modIncludeImplementation
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Cloth Config
	modIncludeImplementation("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}")
	// Mod Menu
	modImplementation("com.terraformersmc:modmenu:${project.mod_menu_version}")
}

shadowJar {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	configurations = [project.configurations.getByName("shade")]
	archiveClassifier.set(null)

	relocate "io.github.ran.ranitils", "io.github.ran.minecraft.ranitils.library.ranitils"
}
jar.finalizedBy(shadowJar)

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
